#
#	The module definition gives its signature for other
#	modules (e.g., the runtime system) that want to execute
#	it.
#
#	The module definition need not export the function init
#	if it is not intended to by loaded by the "conventional loader"
#	(e.g., the OS shell).
#
#
#	This formulation is similar in style to the SlowConvexHull algorithm
#	given on page 3 of de Berg, van Kreveld, Overmars & Schwartzkopf.
#	
#	The sign of the determinant
#	
#			| 1 px py |
#		D =	| 1 qx qy |  = (qx*ry - qy*rx) - px(ry - qy) + py(rx-qx),
#			| 1 rx ry |
#	
#	denotes whether r is on left or right of line pq.
#
#	The module type parameter `coordType` is the type used for sub-coordinate values.
#
ConvexHull : module (coordType: type)
{
	convexHull		: function (inputPoints: list of (coordType, coordType)) -> (convexHull: list of (coordType, coordType));
	convexHull		: probdef (inputPoints: list of (coordType, coordType)) -> (convexHull: list of (coordType, coordType));

	pqDiffer		: predicate (qx:coordType, qy:coordType, px:coordType, py:coordType);

	nonNegativeDeterminant	: predicate (qx:coordType, qy:coordType, rx:coordType, ry:coordType, px:coordType, py:coordType);
	qOnUpperRight		: predicate (qx:coordType, qy:coordType, rx:coordType, ry:coordType, px:coordType, py:coordType);
	qOnUpperLeft		: predicate (qx:coordType, qy:coordType, rx:coordType, ry:coordType, px:coordType, py:coordType);
	qOnLowerLeft		: predicate (qx:coordType, qy:coordType, rx:coordType, ry:coordType, px:coordType, py:coordType);
	qOnLowerRight		: predicate (qx:coordType, qy:coordType, rx:coordType, ry:coordType, px:coordType, py:coordType);
	qOnRight		: predicate (qx:coordType, qy:coordType, rx:coordType, ry:coordType, px:coordType, py:coordType);
	qAbove			: predicate (qx:coordType, qy:coordType, rx:coordType, ry:coordType, px:coordType, py:coordType);
	qOnLeft			: predicate (qx:coordType, qy:coordType, rx:coordType, ry:coordType, px:coordType, py:coordType);
	qBelow			: predicate (qx:coordType, qy:coordType, rx:coordType, ry:coordType, px:coordType, py:coordType);
}

#
#	Several predicate functions for the eventual problem definition
#
pqDiffer : predicate (qx:coordType, qy:coordType, px:coordType, py:coordType) =
{
	!((qy == py) & (qx == px))
}

nonNegativeDeterminant : predicate (qx:coordType, qy:coordType, rx:coordType, ry:coordType, px:coordType, py:coordType) =>
{
	((qx*ry - qy*rx) - px*(ry - qy) + py*(rx - qx)) >= 0
}

qOnUpperRight : predicate (qx:coordType, qy:coordType, rx:coordType, ry:coordType, px:coordType, py:coordType) =>
{
	((qy > py) & (qx < px) & nonNegativeDeterminant(qx, qy, rx, ry)@(px, py))
}

qOnUpperLeft : predicate (qx:coordType, qy:coordType, rx:coordType, ry:coordType, px:coordType, py:coordType) =>
{
	((qy > py) & (qx > px) & nonNegativeDeterminant(qx, qy, rx, ry)@(px, py))
}

qOnLowerLeft : predicate (qx:coordType, qy:coordType, rx:coordType, ry:coordType, px:coordType, py:coordType) =>
{
	((qy < py) & (qx < px) & nonNegativeDeterminant(qx, qy, rx, ry)@(px, py))
}

qOnLowerRight : predicate (qx:coordType, qy:coordType, rx:coordType, ry:coordType, px:coordType, py:coordType) =>
{
	((qy < py) & (qx > px) & nonNegativeDeterminant(qx, qy, rx, ry)@(px, py))
}

qOnRight : predicate (qx:coordType, qy:coordType, rx:coordType, ry:coordType, px:coordType, py:coordType) =>
{
	((qy == py) & (qx > px) & nonNegativeDeterminant(qx, qy, rx, ry)@(px, py))
}

qAbove : predicate (qx:coordType, qy:coordType, rx:coordType, ry:coordType, px:coordType, py:coordType) =>
{
	((qx == px) & (qy > py) & nonNegativeDeterminant(qx, qy, rx, ry)@(px, py))
}

qOnLeft : predicate (qx:coordType, qy:coordType, rx:coordType, ry:coordType, px:coordType, py:coordType) =>
{
	((qy == py) & (qx < px) & nonNegativeDeterminant(qx, qy, rx, ry)@(px, py))
}

qBelow : predicate (qx:coordType, qy:coordType, rx:coordType, ry:coordType, px:coordType, py:coordType) =>
{
	((qx == px) & (qy < py) & nonNegativeDeterminant(qx, qy, rx, ry)@(px, py))
}

#
#	Problem definition. This is a Boolean predicate that should
#	evaluate to 'True' when applied to any output value of the
#	function.
#
convexHull : probdef (inputPoints: list of (coordType, coordType)) -> (convexHull: list of (coordType, coordType)) =>
{
	#
	#	Given a point (px, py) on the list convexHull, there
	#	exists a point (qx, qy) in the list inputPoints (and by
	#	extension, in convexHull) such that for all points
	#	(rx, ry) on the list inputPoints, (rx, ry) is on the left
	#	or the right of the line from (px, py) to (qx, qy) (as
	#	appropriate, by quadrant of the 2D space).
	#
	given ((px, py) in convexHull)
	(
		exists ((qx, qy) in inputPoints)
		(
			forall ((rx, ry) in inputPoints)
			(
				pqDiffer(qx, qy, px, py) &
				(
					  qOnUpperRight	(qx, qy, rx, ry, px, py)
					| qOnUpperLeft	(qx, qy, rx, ry, px, py)
					| qOnLowerLeft	(qx, qy, rx, ry, px, py)
					| qOnLowerRight	(qx, qy, rx, ry, px, py)
					| qOnRight	(qx, qy, rx, ry, px, py)
					| qAbove	(qx, qy, rx, ry, px, py)
					| qOnLeft	(qx, qy, rx, ry, px, py)
					| qBelow	(qx, qy, rx, ry, px, py)
				)
			)
		)
	)
}
