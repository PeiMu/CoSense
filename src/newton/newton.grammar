/*
 *	Lexical elements
 */
reservedIdentifiers		::=	":" | "," | "@" | "o<" | "~" | "<" | "<=" | ">" | ">=" | "==" | "<->"
					| "**" | "*" | "/" | "+" | "-" | "<<" | ">>" | "|" | "++" | "--"
					| "-" | "English" | "constant" | "invariant" | "signal"
					| "sensor" | "name" | "symbol" | "derivation" | "none"
					| "dot" | "cross" | "derivative" | "integral" | "interface"
					| "i2c" | "spi" | "analog" | "write" | "read" | "delay" | "range"
					| "erasuretoken" | "uncertainty" | "accuracy" | "precision"
					| "Gaussian" | "Laplacian" | "StudentT" | "Bernoulli" | "Binomial" 
					| "Poisson" | "NegativeBinomial" | "BetaBinomial" | "Exponential"
					| "Gamma" | "Multinomial" | "Beta" | "LogitNormal" | "Dirichlet"
					| "Cauchy" | "LogNormal" | "Pareto" | "BetaPrime" | "StudentZ" 
					| "Weibull" | "Erlang" | "Maxwell" | "FermiDirac" | "FisherZ" 
					| "LogSeries" | "Gumbel" | "Rayleigh" | "Gibrat" | "PearsonIII" | "ExtremeValue"
					| "F" | "Xi" | "XiSquared" | "to" | "bits" | "><"
					| "dimensionless" | "include" | "(" | ")" | "{" | "}" | ";" | ".." .

zeroToNine			=	"0-9" .
oneToNine			=	"1-9" .
radix				=	oneToNine {zeroToNine} "r" .
charConst			=	"'" character "'" .
integerConst			::=	["+" | "-"] [radix] ("0" | oneToNine {zeroToNine}) | charConst .
drealConst			=	("0" | oneToNine {zeroToNine}) "." {zeroToNine} .
erealConst			=	(drealConst | integerConst) ("e" | "E") integerConst .
realConst			::=	["+" | "-"] (drealConst | erealConst) .
numericConst			::=	integerConst | realConst .
stringConst			::=	'"' {character} '"' .
idchar				=	char-except-rsvopseptoken .
identifier			::=	(idchar-except-zeroToNine) {idchar} .

/*
 *	Syntactic elements
 */
newtonDescription		::=	ruleList .

ruleList			::=	{rule} .
rule				::=	constantDefinition | invariantDefinition | baseSignalDefinition | sensorDefinition .

constantDefinition		::=	identifier ":" "constant" "=" numericConst [unitExpression] ";" .
invariantDefinition		::=	identifier ":" "invariant" parameterTuple  "=" "{" constraintList "}" .
baseSignalDefinition		::=	identifier ":" "signal" [subdimensionTuple] "=" "{" [nameStatement] symbolStatement derivationStatement "}" .
sensorDefinition		::=	identifier ":" "sensor" parameterTuple "=" "{" sensorPropertyList "}" .

nameStatement			::=	"name" "=" stringConst languageSetting ";" .
symbolStatement			::=	"symbol" "=" identifier ";" .
derivationStatement		::=	"derivation" "=" ("none" | "dimensionless" | quantityExpression) ";" .

subdimensionTuple		::=	"(" identifier ":" numericConst "to" numericConst ")" .
parameterTuple			::=	"(" parameter {"," parameter} ")" .
parameter			::=	identifier ":" identifier .

constraintList			::=	[constraint] {"," constraint} .
constraint			::=	quantityExpression comparisonOperator quantityExpression | identifier parameterTuple .

quantityExpression		::=	quantityTerm {lowPrecedenceOperator quantityTerm} .
quantityTerm			::=	[unaryOp] quantityFactor {highPrecedenceOperator quantityFactor} .
quantityFactor			::=	quantity [exponentiationOperator quantityFactor]			|
						timeOperator {timeOperator} quantityFactor			|
						"(" quantityExpression ")" .
						"{" quantityExpression {"," quantityExpression} "}" .
quantity			::=	numericConst | (identifier {"@" numericConst}) .
lowPrecedenceOperator		::=	"+" | "-" .
highPrecedenceOperator		::=	"*" | "/" | "><" | vectorOp .
exponentiationOperator		::=	"**" .
vectorOp			::=	"dot" | "cross" .
timeOperator			::=	"derivative" | "integral" .
comparisonOperator		::=	"o<" | "~" | "<" | "<=" | ">" | ">=" | "==" | "<->" .
languageSetting			::=	"English" .

unitExpression			::=	unitTerm .
unitTerm			::=	unitFactor {highPrecedenceOperator unitFactor} .
unitFactor			::=	[unaryOp] (unit [exponentiationOperator numericConst]) | "(" unitExpression ")" .
unit				::=	identifier .

sensorPropertyList		::=	sensorProperty {"," sensorProperty} .
sensorProperty			::=	rangeStatement | uncertaintyStatement | erasureValueStatement | accuracyStatement | precisionStatement | sensorInterfaceStatement .
sensorInterfaceStatement	::=	"interface" identifier ["@" numericConst "*" "bits"] "==" sensorInterfaceType parameterTuple ["{" sensorInterfaceCommandList "}"] .
sensorInterfaceType		::=	"i2c" | "spi" | "analog" .
sensorInterfaceCommandList	::=	sensorInterfaceCommand {";" sensorInterfaceCommand} .
sensorInterfaceCommand		::=	readRegisterCommand | writeRegisterCommand | delayCommand | arithmeticCommand .
readRegisterCommand		::=	identifier "=" "read" ["[" numericConst "]"] numericConst .
writeRegisterCommand		::=	"write" numericConst numericConst .
delayCommand			::=	"delay" numericConst .
arithmeticCommand		::=	identifier "=" expression .
rangeStatement			::=	"range" identifier "==" "[" numericConst [unitExpression] "," numericConst [unitExpression] "]" .
uncertaintyStatement		::=	"uncertainty" identifier "==" expression [unitExpression] .
erasureValueStatement		::=	"erasuretoken" identifier "==" numericConst [unitExpression] .
accuracyStatement		::=	"accuracy" identifier "==" numericConstTupleList .
precisionStatement		::=	"precision" identifier "==" numericConstTupleList .
numericConstTupleList		::=	"{" numericConstTuple {"," numericConstTuple} "}" .
numericConstTuple		::=	"(" numericConst [unitExpression] ".." numericConst ")" .

expression			::=	term {lowPrecedenceBinaryOp term} .
term				::=	[unaryOp] factor ["++" | "--"] {highPrecedenceBinaryOp factor} .
factor				::=	numericConst | "(" expression ")" | distributionFactor | identifier ["[" numericConst "]"] .
distributionFactor		::=	distribution parameterValueList .
parameterValueList		::=	"(" identifier ":" expression {"," identifier ":" expression} ")" .
distribution			::=	"Gaussian" | "Laplacian" | "StudentT" | "Bernoulli" | "Binomial" 
					| "Poisson" | "NegativeBinomial" | "BetaBinomial" | "Exponential"
					| "Gamma" | "Multinomial" | "Beta" | "LogitNormal" | "Dirichlet"
					| "Cauchy" | "LogNormal" | "Pareto" | "BetaPrime" | "StudentZ" 
					| "Weibull" | "Erlang" | "Maxwell" | "FermiDirac" | "FisherZ" 
					| "LogSeries" | "Gumbel" | "Rayleigh" | "Gibrat" | "PearsonIII" | "ExtremeValue"
					| "F" | "Xi" | "XiSquared" .
highPrecedenceBinaryOp		::=	"*" | "/" | "%" | "**" .
lowPrecedenceBinaryOp		::=	"+" | "-" | ">>" | "<<" | "|" .
unaryOp				::=	"-"  | "+" .
