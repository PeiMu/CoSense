include "sortList-variantA.nd"

InsertionSort : progtype
{
	init		: namegen (args: list of string) -> (results: list of string);
	insert		: namegen (item: string, stringlist: list of string) -> (list of string);

	sort		: namegen (inputList: list of valueType) -> (outputList: list of valueType);
	sort		: probdef (inputList: list of valueType) -> (outputList: list of valueType);
}


init (args: list of string) -> (results: list of string) =
{
	sort <- init;
	init <- sort;
}

sort : namegen (inputList: list of valueType) : (outputList: list of valueType) =
{
	#
	#	Pre-conditions: The init namegen is created, and a valid	
	#		list-of-string input is written to it.
	#
	#	Post-conditions: The reader of the init namegen reads a
	#		list of string whose head is the largest item,
	#		and the items are in non-increasing order from
	#		head to tail, lexicographically.
	#
	result		: list of string;
	insertChannel	:= name2chan (typeof InsertionSort->insert) (string InsertionSort->insert);

	input := <-init;
	iter
	{
		#
		#	Loop invariants: 'result' list is always a sorted
		#		subset of the input list.
		#
		#	Progress: in each iteration, an item is removed
		#		from 'input' and added to 'result'
		#
		#	Termination: when 'input' is empty.
		#
		len input > 0	=>
		{
			item := hd input;
			insertChannel <-= (item, result);
			result = <-insertChannel;
			input = tl input;
		}
	}
}

insert : namegen (item: string, stringlist: list of string) -> (list of string) =
{
	#
	#	Pre-conditions: The insert namegen is created, and a	
	#		valid tuple comprising a single element, and
	#		an already sorted (or empty) list-of-string
	#		input is written to it.
	#
	#	Post-conditions: The reader of the insert  namegen
	#		reads a sorted list of strings.
	#
	result	: list of string;

	(new, inputlist) := <-insert;
	iter
	{
		#
		#	Loop invariant: the list result is always a sorted
		#		list of strings.
		#
		#	Progress: in each iteration, an item is removed from
		#		'inputlist'.
		#
		#	Termination: when 'inputlist' is empty.
		#
		len inputlist > 0	=>
		{
			item = hd inputlist;
			done : bool = false;
			
			matchseq
			{
				item < new || done	=>
				{
					result = item :: result;
				}

				item >= new		=>
				{
					result = new :: result;
					result = item :: result;
					done = true;
				}
			}
			
			inputlist = tl inputlist;
		}
	}
	
	insert <-= result;
}
